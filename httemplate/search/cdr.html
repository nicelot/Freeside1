<& elements/search.html,
               'title' => $title,
               'name'  => 'call detail records',
               'query' => $query,
               'count_query' => $count_query,
               'count_addl' => $nototalminutes ? '' : [ $totalminutes_sub ],
               'disable_total' => $topmode ? 1 : '',
               'header' => \@header,
               'fields' => \@fields,
               'links' => \@links,
               'html_form'   => qq!<FORM NAME="cdrForm" ACTION="$p/misc/cdr.cgi" METHOD="POST">!,
               'html_foot' => $html_foot,
&>
<%init>

die "access denied"
  unless $FS::CurrentUser::CurrentUser->access_right('List rating data');

my $edit_data = $FS::CurrentUser::CurrentUser->access_right('Edit rating data');

my $totalminutes_sub = sub {
    my $billsec = shift;
    sprintf("%.2f",$billsec/60) . ' total minutes';
};

my $conf = new FS::Conf;

my $title = 'Call Detail Records';
my $hashref = {};

#process params for CDR search, populate $hashref...
# and fixup $count_query

my @search = ();

###
# dates
###

my $str2time_sql = str2time_sql;
my $closing      = str2time_sql_closing;

my($beginning, $ending) = FS::UI::Web::parse_beginning_ending($cgi);
push @search, "$str2time_sql calldate $closing >= $beginning ",
              "$str2time_sql calldate $closing <= $ending";

###
# duration / billsec
###

push @search, FS::UI::Web::parse_lt_gt($cgi, 'duration');
push @search, FS::UI::Web::parse_lt_gt($cgi, 'billsec');

#above here things just push @search
#below here things also have to define $hashref->{} or push @qsearch
my @qsearch = @search;

###
# freesidestatus
###

my $status = $cgi->param('freesidestatus');
if ( $status eq 'NULL' ) {

  $title = "Unprocessed $title";
  $hashref->{'freesidestatus'} = ''; # Record.pm will take care of it
  push @search, "( freesidestatus IS NULL OR freesidestatus = '' )";

} elsif ( $status =~ /^([\w ]+)$/ ) {
  
  if ( $status eq 'done' ) {
    $title = "Processed $title";
  }
  elsif ( $status eq 'failed' ) {
    $title = "Skipped $title";
  }
  $hashref->{'freesidestatus'} = $1;
  push @search, "freesidestatus = '$1'";

}

###
# cdrtypenum
###

if ( $cgi->param('cdrtypenum') =~ /^(\d+)$/ ) {
    $hashref->{'cdrtypenum'} = $1;
    push @search, "cdrtypenum = $1";
}
elsif ( $cgi->param('cdrtypenum') eq 'none' ) {
    $hashref->{'cdrtypenum'} = '';
    push @search, "cdrtypenum is null";
}

###
# termpartNstatus
###

foreach my $param ( grep /^termpart\d+status$/, $cgi->param ) {

  my $status = $cgi->param($param);

  $param =~ /^termpart(\d+)status$/ or die 'guru meditation 54something';
  my $termpart = $1;

  my $search = '';
  if ( $status eq 'NULL' ) {

    #false lazienss w/cdr_termination.pm (i should be a part_termination method)
    my $where_term =
      "( cdr.acctid = cdr_termination.acctid AND termpart = $termpart ) ";
    #my $join_term = "LEFT JOIN cdr_termination ON ( $where_term )";
    $search =
      "NOT EXISTS ( SELECT 1 FROM cdr_termination WHERE $where_term )";

  } elsif ( $status =~ /^([\w ]+)$/ ) {

    #false lazienss w/cdr_termination.pm (i should be a part_termination method)
    my $where_term =
      "( cdr.acctid = cdr_termination.acctid AND termpart = $termpart AND status = '$1' ) ";
    #my $join_term = "LEFT JOIN cdr_termination ON ( $where_term )";
    $search =
      "EXISTS ( SELECT 1 FROM cdr_termination WHERE $where_term )";

  }

  if ( $search ) {
    push @search,  $search;
    push @qsearch, $search;
  }

}

###
# src/dest/charged_party/svcnum
###

my $phonenum = qr/^\s*([\d\-\+\ ]+)\s*$/;
my $x = qr/\D/;
if ( $conf->exists('svc_phone-allow_alpha_phonenum') ) {
  $phonenum = qr/^\s*([\d\-\+\ A-Za-z]+)\s*$/;
  $x = qr/[^\dA-Za-z]/;
}

if ( $cgi->param('src') =~ $phonenum ) {
  ( my $src = $1 ) =~ s/$x//g;
  $hashref->{'src'} = $src;
  push @search, "src = '$src'";
}

if ( $cgi->param('dst') ) {

  my @d = map { $_, "1$_" } split(/\s*,\s*/, $cgi->param('dst') );
  
  my $search = 'dst IN ('. join(',', map dbh->quote($_), @d). ')';

  push @search,  $search;
  push @qsearch, $search;

}

if ( $cgi->param('dcontext') =~ /^\s*(.+)\s*$/ ) {
  my $dcontext = $1;
  $hashref->{'dcontext'} = $dcontext;
  push @search, 'dcontext = '. dbh->quote($dcontext);
}

if ( $cgi->param('charged_party') ) {

  my @cp = map { $_, "1$_" }
             split(/\s*,\s*/, $cgi->param('charged_party') );
  
  my $search = 'charged_party IN ('. join(',', map dbh->quote($_), @cp). ')';

  push @search,  $search;
  push @qsearch, $search;
}

if ( $cgi->param('charged_party_or_src') ) {

  my @cp = map { $_, "1$_" }
             split(/\s*,\s*/, $cgi->param('charged_party_or_src') );
  my $in = join(',', map dbh->quote($_), @cp);

  my $search = "( charged_party IN ($in) OR src IN ($in) )";

  push @search,  $search;
  push @qsearch, $search;
}

if ( $cgi->param('lastapp') =~ /^\s*(.+)\s*$/ ) {
  my $lastapp = $1;
  $hashref->{'lastapp'} = $lastapp;
  push @search, 'lastapp = '. dbh->quote($lastapp);
}

if ( $cgi->param('svcnum') =~ /^([\d, ]+)$/ ) {
  my $svcnum = $1;
  my $search = "svcnum IN ($svcnum)";
  push @search,  $search;
  push @qsearch, $search;
}

###
# src/dst_ip_addr
###
foreach my $field ('src_ip_addr','dst_ip_addr') {
  if ( $cgi->param($field) ) {
    my $search = FS::cdr->ip_addr_sql($field, $cgi->param($field));
    push @search, $search;
    push @qsearch, $search;
  }
}

###
# cdrbatchnum (or legacy cdrbatch)
###

if ( $cgi->param('cdrbatch') ) {

  my $cdr_batch =
    qsearchs('cdr_batch', { 'cdrbatch' => scalar($cgi->param('cdrbatch')) } );
  if ( $cdr_batch ) {
    $hashref->{cdrbatchnum} = $cdr_batch->cdrbatchnum;
    push @search, 'cdrbatchnum = '. $cdr_batch->cdrbatchnum;
  } else {
    die "unknown cdrbatch ". $cgi->param('cdrbatch');
  }

} elsif ( $cgi->param('cdrbatchnum') ne '__ALL__' ) {

  if ( $cgi->param('cdrbatchnum') eq '' ) {
    my $search = "( cdrbatchnum IS NULL )";
    push @qsearch, $search;
    push @search,  $search;
  } elsif ( $cgi->param('cdrbatchnum') =~ /^(\d+)$/ ) {
    $hashref->{cdrbatchnum} = $1;
    push @search, "cdrbatchnum = $1";
  }

}

###
# acctid
###

if ( $cgi->param('acctid') =~ /\d/ ) {
  my $acctid = $cgi->param('acctid');
  $acctid =~ s/\r\n/\n/g; #browsers?
  my @acctid = map  { /^\s*(\d+)\s*$/ or die "guru meditation #4"; $1; }
               grep { /^\s*(\d+)\s*$/ }
               split(/\n/, $acctid);
  if ( @acctid ) {
    my $search = 'acctid IN ( '. join(',', @acctid). ' )';
    push @qsearch, $search;
    push @search,  $search;
  }
}

###
# accountcode
###

if ( $cgi->param('accountcode') =~ /\S/ ) {
  my $accountcode = $cgi->param('accountcode');
  my @accountcode = map {
    ( my $v = $_ ) =~ s/^\s+|\s+$//g;    # trim margin whitespace
    length $v ? dbh->quote($v) : ()
  } grep /\S/, split /\R/, $accountcode;    # collect non-trivial lines
  if (@accountcode) {
    my $search = 'accountcode IN ( ' . join( ',', @accountcode ) . ' )';
    push @qsearch, $search;
    push @search,  $search;
  }
}

###
# finish it up
###

my $nototalminutes = 0;

my $search = join(' AND ', @search);
$search = "WHERE $search" if $search;

my $count_query = "SELECT COUNT(*), sum(billsec) FROM cdr $search";

my $qsearch = join(' AND ', @qsearch);
$qsearch = ( scalar(keys %$hashref) ? ' AND ' : ' WHERE ' ) . $qsearch
  if $qsearch;

my $query =     {   'table'     => 'cdr',
                    'hashref'   => $hashref,
			        'extra_sql' => $qsearch,
			        'order_by'  => 'ORDER BY calldate',
                };

if ( $cgi->param('longest') =~ /^(\d+)$/ && $cgi->param('longest') > 0 ) {
    $cgi->param('maxrecords',$1);
    $count_query = "SELECT $1";
    $query->{'order_by'} = 'ORDER BY billsec desc';
    $nototalminutes = 1;
}


###
# display fields
###

my %header = %{ FS::cdr->table_info->{'fields'} };

my @first = qw( acctid calldate clid charged_party src dst dcontext );
my %first = map { $_=>1 } @first;

my @fields = ( @first, grep !$first{$_}, fields('cdr') );

if ( $cgi->param('show') ) {
  @fields = grep $cgi->param("show_$_"), @fields;
}

my @header = map {
                   if ( exists($header{$_}) ) {
                     $header{$_};
                   } else {
                     my $header = $_;
                     $header =~ s/\_/ /g; #//wtf
                     ucfirst($header);
                   }
                 } @fields;
unshift @header, ' ';

my $date_sub_factory = sub {
  my $column = shift;
  sub {
    #my $cdr = shift;
    my $date = shift->$column();
    $date ? time2str( '%Y-%m-%d %T', $date ) : ''; #config time2str format?
  };
};

my %fields = (
  #any other formatters?
  map { $_ => &{ $date_sub_factory }($_) } qw( startdate answerdate enddate )
);

my %links = (
  'svcnum' =>
    sub { $_[0]->svcnum ? [ $p.'view/svc_phone.cgi?', 'svcnum' ] : ''; },
);

@fields = map { exists($fields{$_}) ? $fields{$_} : $_ } @fields;
unshift @fields, sub {
                       return '' unless $edit_data;
                       my $cdr = shift;
                       my $acctid = $cdr->acctid;
                       qq!<INPUT NAME="acctid$acctid" TYPE="checkbox" VALUE="1">!;
                     };

my @links = ( map { exists($links{$_}) ? $links{$_} : '' } @fields );


###
# top dst / top src
###

my $topmode = 0;

if ( $cgi->param('top_dst') =~ /^(\d+)$/ && $cgi->param('top_dst') > 0 ) {
        $topmode = $1;
        @fields = ('dst');
        @header = ('Destination');
}
elsif ( $cgi->param('top_src') =~ /^(\d+)$/ && $cgi->param('top_src') > 0 ) {
        $topmode = $1;
        @fields = ('src');
        @header = ('Source');
}

if ( $topmode ) {
    my $whichfield = $fields[0];
    $query = {  'select'    => "$whichfield, count(1) as cnt",
                'table'     => 'cdr',
                'hashref'   => {},
                'extra_sql' => "$search
                                group by $whichfield
                                order by cnt desc",
             };
    $count_query = "SELECT $topmode";

    # this is weird, maybe we should change search.html
    $cgi->param('maxrecords',$topmode);
    
    # kind of hacked
    my $link = "${p}search/cdr.html?".$cgi->query_string;
    $link =~ s/;top_(dst|src)=(\d+|)//g;
    $link =~ s/;maxrecords=(\d+|)//;
    $link =~ s/;(src|dst)=(\d+|)//g;
    $link = [ "$link;$whichfield=", sub { shift->$whichfield } ];
    @links = ($link);

    push @fields, 'cnt';
    push @header, '# Calls';
    
    $nototalminutes = 1;
}

my $html_foot = include('/search/elements/checkbox-foot.html',
  actions => [
    { submit  => "reprocess selected",
      name    => "action",
      confirm => "Are you sure you want to reprocess the selected CDRs?" },
    { submit  => "delete selected",
      name    => "action",
      confirm => "Are you sure you want to delete the selected CDRs?" },
  ]
);
</%init>
