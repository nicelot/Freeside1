<%doc>

Example:

  <& elements/search.html,

    ###
    # required
    ###

    'title'         => 'Page title',
    
    'name_singular' => 'item',  #singular name for the records returned
       #OR#                     # (preferred, will be pluralized automatically)
    'name'          => 'items', #plural name for the records returned
                                # (deprecated, will be singularlized
                                #  simplisticly)

    #literal SQL query string (deprecated?) or qsearch hashref or arrayref
    #of qsearch hashrefs for a union of qsearches
    'query'       => {
                       'table'     => 'tablename',
                       #everything else is optional...
                       'hashref'   => { 'field' => 'value',
                                        'field' => { 'op'    => '<',
                                                     'value' => '54',
                                                   },
                                      },
                       'select'    => '*',
                       'addl_from' => '', #'LEFT JOIN othertable USING ( key )',
                       'extra_sql' => '', #'AND otherstuff', #'WHERE onlystuff',
                       'order_by'  => 'ORDER BY something',
   
                     },
                     # "select * from tablename";
   
    #required unless 'query' is an SQL query string (shouldn't be...)
    'count_query' => 'SELECT COUNT(*) FROM tablename',

    ###
    # recommended / common
    ###

    #listref of column labels, <TH>
    #recommended unless 'query' is an SQL query string
    # (if not specified the database column names will be used) (XXX this is not currently working either)
    'header'      => [ '#',
                       'Item',
                       { 'label' => 'Another Item',
                         
                       },
                     ],

    #listref - each item is a literal column name (or method) or coderef
    #if not specified all columns will be shown (XXX this is not currently working?)
    'fields'      => [
                       'column',
                       sub { my $row = shift; $row->column; },
                     ],

    #redirect if there's only one item...
    # listref of URL base and column name (or method)
    # or a coderef that returns the same
    'redirect' => sub { my( $record, $cgi ) = @_;
                        [ popurl(2).'view/item.html', 'primary_key' ];
                      },

    #redirect if there's no items
    # scalar URL or a coderef that returns a URL
    'redirect_empty' => sub { my( $cgi ) = @_;
                              popurl(2).'view/item.html';
                            },
   
    ###
    # optional
    ###
   
    # some HTML callbacks...
    'menubar'          => '', #menubar arrayref
    'html_init'        => '', #after the header/menubar and before the pager
    'html_form'        => '', #after the pager, right before the results
                              # (only shown if there are results)
                              # (use this for any form-opening tag rather than
                              #  html_init, to avoid a nested form)
    'html_foot'        => '', #at the bottom
    'html_posttotal'   => '', #at the bottom
                              # (these three can be strings or coderefs)
    
    'count_addl' => [], #additional count fields listref of sprintf strings or coderefs
                        # [ $money_char.'%.2f total paid', ],
   
    #second (smaller) header line, currently only for HTML
    'header2      => [ '#',
                       'Item',
                       { 'label' => 'Another Item',
                         
                       },
                     ],

    #listref of column footers
    'footer'      => [],
    
    #disabling things
    'disable_download'  => '', # set true to hide the CSV/Excel download links
    'disable_total'     => '', # set true to hide the total"
    'disable_maxselect' => '', # set true to disable record/page selection
    'disable_nonefound' => '', # set true to disable the "No matching Xs found"
                               # message
   
    #handling "disabled" fields in the records
    'disableable' => 1,  # set set to 1 (or column position for "disabled"
                         # status col) to enable if this table has a "disabled"
                         # field, to hide disabled records & have
                         # "show disabled/hide disabled" links
                         #(can't be used with a literal query)
    'disabled_statuspos' => 3, #optional position (starting from 0) to insert
                               #a Status column when showing disabled records
                               #(query needs to be a qsearch hashref and
                               # header & fields need to be defined)

    #handling agent virtualization
    'agent_virt'            => 1, # set true if this search should be
                                  # agent-virtualized
    'agent_null'            => 1, # set true to view global records always
    'agent_null_right'      => 'Access Right', # optional right to view global
                                               # records
    'agent_null_right_link' => 'Access Right'  # optional right to link to
                                               # global records; defaults to
                                               # same as agent_null_right
    'agent_pos'             => 3, # optional position (starting from 0) to
                                  # insert an Agent column (query needs to be a
                                  # qsearch hashref and header & fields need to
                                  # be defined)cust_pkg_susp.html

    # sort, link & display properties for fields

    'sort_fields' => [], #optional list of field names or SQL expressions for
                         # sorts
   
    #listref - each item is the empty string,
    #          or a listref of link and method name to append,
    #          or a listref of link and coderef to run and append
    #          or a coderef that returns such a listref
    'links'       => [],

    #listref - each item is the empty string,
    #          or a string onClick handler for the corresponding link
    #          or a coderef that returns string onClick handler
    'link_onclicks' => [],

    #one letter for each column, left/right/center/none
    # or pass a listref with full values: [ 'left', 'right', 'center', '' ]
    'align'       => 'lrc.',
   
    #listrefs of ( scalars or coderefs )
    # currently only HTML, maybe eventually Excel too
    'color'       => [],
    'size'        => [],
    'style'       => [], #<B> or <I>, etc.
    'cell_style'  => [], #STYLE= attribute of TR, very HTML-specific...

    # Excel-specific listref of ( hashrefs or coderefs )
    # each hashref: http://search.cpan.org/dist/Spreadsheet-WriteExcel/lib/Spreadsheet/WriteExcel.pm#Format_methods_and_Format_properties
    'xls_format' => => [],
   

    # miscellany
   'download_label' => 'Download this report',
                        # defaults to 'Download full results' 
   'link_field'     => 'pkgpart'
                        # will create internal links for each row,
                        # with the value of this field as the NAME attribute
                        # If this is a coderef, will evaluate it, passing the
                        # row as an argument, and use the result as the NAME.
  &>

</%doc>
% if ( $type eq 'csv' ) {
%
<% include('search-csv.html',  header=>$header, rows=>$rows, opt=>\%opt ) %>
%
% } elsif ( $type =~ /\.xls$/ ) {
%
<& 'search-xls.html',  header=>$header, rows=>$rows, opt=>\%opt &>\
% # prevent the caller from polluting our output stream
% $m->abort;
%
% } elsif ( $type eq 'xml' ) {
%
<% include('search-xml.html',  rows=>$rows, opt=>\%opt ) %>
%
% } else {
%
<% include('search-html.html',
             type           => $type,
             header         => $header,
             rows           => $rows,
             link_agentnums => \@link_agentnums,
             null_link      => $null_link,
             confmax        => $confmax,
             maxrecords     => $maxrecords,
             offset         => $offset,
             opt            => \%opt
          )
%>
%
% } 
<%init>

my(%opt) = @_;
#warn join(' / ', map { "$_ => $opt{$_}" } keys %opt ). "\n";

my $curuser = $FS::CurrentUser::CurrentUser;

my $type = $cgi->param('_type') =~ /^(csv|\w*\.xls|xml|select|html(-print)?)$/
           ? $1 : 'html' ;

if ( !$curuser->access_right('Download report data') ) {
  $opt{'disable_download'} = 1;
  $type = 'html';
}

my %align = (
  'l' => 'left',
  'r' => 'right',
  'c' => 'center',
  ' ' => '',
  '.' => '',
);
$opt{align} = [ map $align{$_}, split(//, $opt{align}) ],
  unless !$opt{align} || ref($opt{align});

if($type =~ /csv|xls/) {
  my $h = $opt{'header'};
  my @del;
  my $i = 0;
  do {
    if( ref($h->[$i]) and exists($h->[$i]->{'nodownload'}) ) {
      splice(@{$opt{$_}}, $i, 1) foreach
        qw(header footer fields links link_onclicks
           align color size style cell_style xls_format);
    }
    else {
      $i++;
    }
  } while ( exists($h->[$i]) );
}

# wtf?
$opt{disable_download} = 0
  if $opt{disable_download} && $curuser->access_right('Configuration download');

$opt{disable_download} = 1
  if $opt{really_disable_download};

my @link_agentnums = ();
my $null_link = '';
if ( $opt{'agent_virt'} ) {

  @link_agentnums = $curuser->agentnums;
  $null_link = $curuser->access_right(    $opt{'agent_null_right_link'}
                                       || $opt{'agent_null_right'}      );

  my $agentnums_sql = $curuser->agentnums_sql(
                        'null'       => $opt{'agent_null'},
                        'null_right' => $opt{'agent_null_right'},
                        'table'      => $opt{'query'}{'table'},
                      );

  $opt{'query'}{'extra_sql'} .=
    ( $opt{'query'}{'extra_sql'} =~ /WHERE/i || keys %{$opt{'query'}{'hashref'}}
      ? ' AND '
      : ' WHERE ' ). $agentnums_sql;

  $opt{'count_query'} .=
    ( $opt{'count_query'} =~ /WHERE/i ? ' AND ' : ' WHERE ' ).  $agentnums_sql;

  if ( $opt{'agent_pos'} || $opt{'agent_pos'} eq '0'
       and scalar($curuser->agentnums) > 1           ) {
    #false laziness w/statuspos above
    my $pos = $opt{'agent_pos'};

    foreach my $att (qw( align color size style cell_style xls_format )) {
      $opt{$att} ||= [ map '', @{ $opt{'fields'} } ];
    }

    splice @{ $opt{'header'} }, $pos, 0, 'Agent'; 
    splice @{ $opt{'align'}  }, $pos, 0, 'c'; 
    splice @{ $opt{'style'}  }, $pos, 0, ''; 
    splice @{ $opt{'size'}   }, $pos, 0, ''; 
    splice @{ $opt{'fields'} }, $pos, 0,
      sub { $_[0]->agentnum ? $_[0]->agent->agent : '(global)'; };
    splice @{ $opt{'color'}  }, $pos, 0, '';
    splice @{ $opt{'links'}  }, $pos, 0, '' #[ 'agent link?', 'agentnum' ]
      if $opt{'links'};
    splice @{ $opt{'link_onclicks'}  }, $pos, 0, ''
      if $opt{'link_onclicks'};

  }

}

if ( $opt{'disableable'} ) {

  unless ( $cgi->param('showdisabled') ) { #modify searches

    $opt{'query'}{'hashref'}{'disabled'} = '';
    $opt{'query'}{'extra_sql'} =~ s/^\s*WHERE/ AND/i;

    $opt{'count_query'} .=
      ( $opt{'count_query'} =~ /WHERE/i ? ' AND ' : ' WHERE ' ).
      "( disabled = '' OR disabled IS NULL )";

  } elsif (    $opt{'disabled_statuspos'}
            || $opt{'disabled_statuspos'} eq '0' ) { #add status column

    my $pos = $opt{'disabled_statuspos'};

    foreach my $att (qw( align style color size )) {
      $opt{$att} ||= [ map '', @{ $opt{'fields'} } ];
    }

    splice @{ $opt{'header'} }, $pos, 0, 'Status'; 
    splice @{ $opt{'align'}  }, $pos, 0, 'c'; 
    splice @{ $opt{'style'}  }, $pos, 0, 'b'; 
    splice @{ $opt{'size'}   }, $pos, 0, ''; 
    splice @{ $opt{'fields'} }, $pos, 0,
      sub { shift->disabled ? 'DISABLED' : 'Active'; };
    splice @{ $opt{'color'}  }, $pos, 0,
      sub { shift->disabled ? 'FF0000'   : '00CC00'; };
    splice @{ $opt{'links'}  }, $pos, 0, ''
      if $opt{'links'};
    splice @{ $opt{'link_onlicks'}  }, $pos, 0, ''
      if $opt{'link_onlicks'};
  }

  #add show/hide disabled links
  my $items = $opt{'name'} || PL($opt{'name_singular'});
  if ( $cgi->param('showdisabled') ) {
    $cgi->param('showdisabled', 0);
    $opt{'html_posttotal'} .=
      '( <a href="'. $cgi->self_url. qq!">hide disabled $items</a> )!; #"
    $cgi->param('showdisabled', 1);
  } else {
    $cgi->param('showdisabled', 1);
    $opt{'html_posttotal'} .=
      '( <a href="'. $cgi->self_url. qq!">show disabled $items</a> )!; #"
    $cgi->param('showdisabled', 0);
  }

}

my $limit = '';
my($confmax, $maxrecords, $offset );

unless ( $type =~ /^(csv|xml|\w*.xls)$/) {
# html mode
  unless (exists($opt{count_query}) && length($opt{count_query})) {
    ( $opt{count_query} = $opt{query} ) =~
      s/^\s*SELECT\s*(.*?)\s+FROM\s/SELECT COUNT(*) FROM /i; #silly vim:/
  }

  if ( $opt{disableable} && ! $cgi->param('showdisabled') ) {
    $opt{count_query} .=
      ( ( $opt{count_query} =~ /WHERE/i ) ? ' AND ' : ' WHERE ' ).
      "( disabled = '' OR disabled IS NULL )";
  }

  unless ( $type eq 'html-print' ) {

    #setup some pagination things if we're in html mode

    my $conf = new FS::Conf;
    $confmax = $conf->config('maxsearchrecordsperpage');
    if ( $cgi->param('maxrecords') =~ /^(\d+)$/ ) {
      $maxrecords = $1;
    } else {
      $maxrecords ||= $confmax;
    }

    $opt{'disable_maxselect'} ||= $conf->exists('disable_maxselect');

    $limit = $maxrecords ? "LIMIT $maxrecords" : '';

    $offset = $cgi->param('offset') =~ /^(\d+)$/ ? $1 : 0;
    $limit .= " OFFSET $offset" if $offset;

  }

}

#order by override
my $order_by = '';
#if ( $cgi->param('order_by') =~ /^([\w\, ]+)$/ ) {
#  $order_by = $1;
#}
$order_by = $cgi->param('order_by') if $cgi->param('order_by');

# run the query

my $header = [ map { ref($_) ? $_->{'label'} : $_ } @{$opt{header}} ];
my $rows;
if ( ref($opt{query}) ) {

  my @query;
  if (ref($opt{query}) eq 'HASH') {
    @query = ( $opt{query} );

    if ( $order_by ) {
      if ( $opt{query}->{'order_by'} ) {
        if ( $opt{query}->{'order_by'} =~ /^(\s*ORDER\s+BY\s+)?(\S.*)$/is ) {
          $opt{query}->{'order_by'} = "ORDER BY $order_by, $2";
        } else {
          warn "unparsable query order_by: ". $opt{query}->{'order_by'};
          die "unparsable query order_by: ". $opt{query}->{'order_by'};
        }
      } else {
        $opt{query}->{'order_by'} = "ORDER BY $order_by";
      }
    }

  } elsif (ref($opt{query}) eq 'ARRAY') {
    @query = @{ $opt{query} };
  } else {
    die "invalid query reference";
  }

  if ( $opt{disableable} && ! $cgi->param('showdisabled') ) {
    #%search = ( 'disabled' => '' );
    $opt{'query'}->{'hashref'}->{'disabled'} = '';
    $opt{'query'}->{'extra_sql'} =~ s/^\s*WHERE/ AND/i;
  }

  #eval "use FS::$opt{'query'};";
  my @param = qw( select table addl_from hashref extra_sql order_by debug );
  $rows = [ qsearch( [ map { my $query = $_;
                             ({ map { $_ => $query->{$_} } @param });
                           }
                       @query
                     ],
                     'order_by' => $opt{order_by}. " ". $limit,
                   )
          ]; 
} else {
  my $sth = dbh->prepare("$opt{'query'} $limit")
    or die "Error preparing $opt{'query'}: ". dbh->errstr;
  $sth->execute
    or die "Error executing $opt{'query'}: ". $sth->errstr;

  #can get # of rows without fetching them all?
  $rows = $sth->fetchall_arrayref;

  $header ||= $sth->{NAME};
}

push @$rows, $opt{'footer_data'} if $opt{'footer_data'};

</%init>
